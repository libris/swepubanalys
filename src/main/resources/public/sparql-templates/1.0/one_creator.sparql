# Publication list of a Creator
PREFIX swpa_m: <http://swepub.kb.se/SwePubAnalysis/model#>
PREFIX mods_m: <http://swepub.kb.se/mods/model#> 
PREFIX xlink: <http://www.w3.org/1999/xlink#> 
SELECT DISTINCT
?_name #Upphov
?_orcid #ORCID (*)
?_titleValue #Titel (*)
?_orgCode #Organisation (*)
?_affiliation #Affiliering  (*) #-
?_pubYear #Publiceringsår
?_publicatType #Publikationstyp/output (*)
?_status #Publiceringsstatus #-
?_channel #Värdpublikation (*)
?_issn #ISSN (*) #-
?_OA #OA typ (*) #-
?_subject #Ämne nivå 2 (*) -
?_uri #Länk till arkiv (*) #-
?_fulltext #Fulltextlänk (*) #-
?_isiValue #ISI ID (*) #-
?_doiValue #DOI (*) #-
?_scopusValue #Scopus ID #-
?_pmidValue #Pubmed ID #-
?_recordID #Record ID
?_dubblettID #Dubblett ID #-
WHERE
{
?Record a mods_m:Record .
?Mods a mods_m:Mods .

?Mods mods_m:identifierValue ?_recordID .

#START_<?_status>
?Record swpa_m:publicationStatus ?_status . 
#END_<?_status>

#START_<?_titleValue>
?Mods mods_m:hasTitleInfo ?TitleInfo .
MINUS { ?TitleInfo mods_m:type 'alternative'^^xsd:string }
?TitleInfo mods_m:hasTitle ?Title .
?Title mods_m:titleValue ?_titleValue .
#END_<?_titleValue>

#START_<?_uri>	
?Record swpa_m:uriRepository ?_uri .
#END_<?_uri>

#START_<?_fulltext>
OPTIONAL { ?Record swpa_m:uriFulltext ?_fulltext . }
#END_<?_fulltext>

#START_<?_doiValue>
OPTIONAL
{
?Mods mods_m:hasIdentifier ?DOI .
?DOI mods_m:doiValue ?_doiValue .
}
#END_<?_doiValue>

#START_<?_isiValue>
OPTIONAL
{
?Mods mods_m:hasIdentifier ?ISI .
?ISI mods_m:isiValue ?_isiValue .
}
#END_<?_isiValue>

#START_<?_scopusValue>
OPTIONAL
{
?Mods mods_m:hasIdentifier ?Scopus .
?Scopus mods_m:scopusValue ?_scopusValue .
}
#END_<?_scopusValue>

#START_<?_pmidValue>
OPTIONAL
{
?Mods mods_m:hasIdentifier ?PMID .
?PMID mods_m:pmidValue ?_pmidValue .
}
#END_<?_pmidValue>

#START_<?_channel>
OPTIONAL { ?Mods swpa_m:titleValueRelatedItem ?_channel }
#END_<?_channel>
#START_<?_issn>
OPTIONAL { ?Mods swpa_m:issnRelatedItem ?_issn }
#END_<?_issn>

#START_<?_affiliation>
OPTIONAL
{
?Name mods_m:hasAffiliation ?Affiliation .
?Affiliation mods_m:authority 'kb.se'^^xsd:string .
?Affiliation mods_m:valueURI ?_affiliation .
}
#END_<?_affiliation>

#START_<?_dubblettID>
OPTIONAL
{
?Record swpa_m:possiblySameAs ?Dubblett .
?Dubblett mods_m:hasMods ?Mods2 .
?Mods2 mods_m:identifierValue ?_dubblettID .
}
#END_<?_dubblettID>
{
SELECT DISTINCT ?Record ?Mods ?_pubYear ?_orgCode ?_publicatType ?_hsv3 ?_subject ?_OA ?Name ?_name ?_orcid
WHERE
{
?Record mods_m:hasMods ?Mods .
?Record a mods_m:Record .
?Mods a mods_m:Mods .

?Mods swpa_m:publicationYear ?_pubYear .
?Mods mods_m:recordContentSourceValue ?_orgCode .

?Mods swpa_m:publicationTypeCode ?_publicatType .
?Record swpa_m:isPublished ?_isPublished .
OPTIONAL { ?Mods swpa_m:oaType ?_OA }

#START_<?_hsv3>
OPTIONAL
{ 
?Mods mods_m:hasSubject ?Subject .
?Subject swpa_m:hsv3 ?_hsv3 .
GRAPH <http://www.ub.uu.se/epub/categories/hsv#graph> 
{ 
?Class a owl:Class . 
}
?Class swpa_m:id ?_hsv3  . 
?Class rdfs:label ?_subject . 
?Class swpa_m:level "3"^^xsd:integer . 
FILTER ( lang(?_subject) = 'sv') 
}
#END_<?_hsv3>

###  FILTERS START ####
#START_<ONE_CREATOR_NAME_FILTER>
?Mods mods_m:hasName ?Name . 
?Name foaf:name ?_name .
#FILTER_<?_name> ( bif:contains(?_name, <?_name> ) )
OPTIONAL { ?Name swpa_m:orcid ?_orcid }
#END_<ONE_CREATOR_NAME_FILTER>

#START_<ONE_CREATOR_ORCID_FILTER>
?Mods mods_m:hasName ?Name . 
?Name foaf:name ?_name .
?Name swpa_m:orcid ?_orcid .
#FILTER_<?_orcid> ( xsd:string(?_orcid) = <?_orcid> )
#END_<ONE_CREATOR_ORCID_FILTER>

#START_<ONE_CREATOR_TWO_FILTERS>
{
?Mods mods_m:hasName ?Name . 
?Name foaf:name ?_name .
OPTIONAL { ?Name swpa_m:orcid ?_orcid }
#FILTER_<?_name> ( bif:contains(?_name, <?_name> ) )
}
UNION
{
?Mods mods_m:hasName ?Name . 
?Name foaf:name ?_name .
?Name swpa_m:orcid ?_orcid .
#FILTER_<?_orcid> ( xsd:string(?_orcid) = <?_orcid> )
}
#END_<ONE_CREATOR_TWO_FILTERS>

#FILTER_<?_orgCode> ( xsd:string(?_orgCode) IN ( <?_orgCode> ) ) 
#FILTER_<?_pubYear_low> ( ?_pubYear >= <?_pubYear_low> ) 
#FILTER_<?_pubYear_high> ( ?_pubYear <= <?_pubYear_high> ) 
#FILTER_<?_publicatType> (xsd:string(?_publicatType) IN ( <?_publicatType> ) ) 
#FILTER_<?_isPublished> ( ?_isPublished = <?_isPublished> ) 
#FILTER_<?_hsv3> ( xsd:int(?_hsv3) IN ( <?_hsv3> ) )
#FILTER_<?_OA> ( BOUND(?_OA ))
###  FILTERS END ####
}
LIMIT 500
}
}
LIMIT 100
