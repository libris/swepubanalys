#Data Violation list query
PREFIX swpa_m: <http://swepub.kb.se/SwePubAnalysis/model#>
PREFIX mods_m: <http://swepub.kb.se/mods/model#> 
SELECT DISTINCT
?_recordID #Record ID
?_oaipmh_SwePub #Länk till MODS-post i SwePub
?_orgCode #Organisation
?_violationType #Problemtyp kod #-
?_violationTypeLabel #Problemtyp namn
?_incorrect #Bristfällig data
?_severity #Nivå
?_pubYear #Publiceringsår
?_isPublished #Publicerad
?_publicatType #Publikationstyp/output
?_uri #Länk till arkiv
WHERE
{
#START_<?_publicatType>
?Mods swpa_m:publicationTypeCode ?_publicatType .
#END_<?_publicatType>
#START_<?_isPublished>
?Record swpa_m:isPublished ?_isPublished .
#END_<?_isPublished>
#START_<?_uri>
?Mods mods_m:hasIdentifier ?Identifier .
?Identifier mods_m:type ?_uriType .
FILTER ( xsd:string(?_uriType) = 'uri')
?Identifier mods_m:identifierValue ?_uri .
#END_<?_uri>

{
SELECT DISTINCT ?Record ?Mods ?_recordID ?_pubYear ?_orgCode ?_violationType ?_violationTypeLabel ?_incorrect ?_severity
#START_<_oaipmh_SwePub>
bif:sprintf('http://api.libris.kb.se/swepub/oaipmh?verb=GetRecord&identifier=%s&metadataPrefix=swepub_mods', ?_recordID ) as ?_oaipmh_SwePub
#END_<_oaipmh_SwePub>
WHERE
{
?Record mods_m:hasMods ?Mods .
?Record a mods_m:Record .
?Mods a mods_m:Mods .
?Mods mods_m:identifierValue ?_recordID .

?DataQualityViolation swpa_m:relatedRecord ?Record . 
?DataQualityViolation a swpa_m:DataQualityViolation . 
?DataQualityViolation a ?_violationType. 
FILTER ( ?_violationType != swpa_m:DataQualityViolation )
#START_<_incorrect>
OPTIONAL { ?DataQualityViolation swpa_m:violatingData ?_incorrect . } 
#END_<_incorrect>
#START_<?_severity>
?_violationType swpa_m:severity ?_severity .
#END_<?_severity>
#START_<?_violationTypeLabel>
?_violationType rdfs:label ?_violationTypeLabel .
#END_<?_violationTypeLabel>

?Mods swpa_m:publicationYear ?_pubYear .
?Mods mods_m:recordContentSourceValue ?_orgCode .

###  FILTERS START ####
#FILTER_<?_orgCode> ( xsd:string(?_orgCode) IN ( <?_orgCode> ) ) 
#FILTER_<?_pubYear_low> ( ?_pubYear >= <?_pubYear_low> ) 
#FILTER_<?_pubYear_high> ( ?_pubYear <= <?_pubYear_high> ) 
#FILTER_<?_violationType> ( ?_violationType IN ( <?_violationType> ))
###  FILTERS END ####
}
LIMIT 500
}
}
LIMIT 100
