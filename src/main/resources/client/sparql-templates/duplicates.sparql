# Dubblett lista
PREFIX mods_m: <http://swepub.kb.se/mods/model#>
PREFIX swpa_m: <http://swepub.kb.se/SwePubAnalysis/model#>
SELECT
?_orgCode1 #Organisation för post 1
?_orgCode2 #Organisation för post 2
?_total_match_weight #Viktning
?_uri1 #Länk till post 1
?_uri2 #Länk till post 2
WHERE
{
?Record1 mods_m:hasMods ?Mods1 .
?Mods1 mods_m:recordContentSourceValue ?_orgCode1 .
?Record2 mods_m:hasMods ?Mods2 .
?Mods2 mods_m:recordContentSourceValue ?_orgCode2 .
?Record1 swpa_m:uriRepository ?_uri1 .
?Record2 swpa_m:uriRepository ?_uri2 .
{
SELECT DISTINCT
sum(?_weight) as ?_total_match_weight
?Record1 ?Record2
WHERE
{
?Match swpa_m:weight ?_weight .
{
SELECT DISTINCT 
?Record1 ?Record2 ?Match 
?_orgCode1 
xsd:string(?_orgCode2) as ?_orgCode2 
?_pubYear 
WHERE
{
?Record1 a swpa_m:DeduplicationEligible .
?Record1 a mods_m:Record .
?Record1 mods_m:hasMods ?Mods1 .
?Mods1 swpa_m:publicationYear ?_pubYear .
?Mods1 mods_m:recordContentSourceValue ?_orgCode1 .

?Mods1 swpa_m:hasMatch ?Match .
?Match swpa_m:hasTarget ?Mods2 .

?Record2 mods_m:hasMods ?Mods2 .
?Record2 a mods_m:Record .
?Mods2 mods_m:recordContentSourceValue ?_orgCode2 .
MINUS
{
?Adjudication a swpa_m:Adjudication .
?Adjudication swpa_m:compares ?Record1 .
?Adjudication swpa_m:compares ?Record2 .
?Adjudication swpa_m:isDuplicate 0 .
}
###  FILTERS START ####
#FILTER_<?_orgCode1> ( ?_orgCode1 IN ( <?_orgCode1>^^xsd:string ) ) 
#FILTER_<?_pubYear_low> ( ?_pubYear >= <?_pubYear_low> ) 
#FILTER_<?_pubYear_high> ( ?_pubYear <= <?_pubYear_high> ) 
###  FILTERS END ####
}
LIMIT 10000
}
###  FILTERS START ####
#FILTER_<?_orgCode2> ( ?_orgCode2 IN ( <?_orgCode2> ) ) 
###  FILTERS END ####
}
GROUP BY ?Record1 ?Record2 
HAVING ( sum( ?_weight )  >= 0.8 && sum( ?_weight ) <= 1.1 ) 
#HAVING ( sum( ?_weight )  >= <?_total_match_weight_low> && sum( ?_weight ) <= <?_total_match_weight_high> ) 
}
}
ORDER BY DESC( ?_total_match_weight) 

